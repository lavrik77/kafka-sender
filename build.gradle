import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '2.7.3' apply false
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java-library'
	id 'maven-publish'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencyManagement {
	imports {
		mavenBom SpringBootPlugin.BOM_COORDINATES
	}
}

publishing {
	publications {
		library(MavenPublication) {
			from components.java
		}
	}
	repositories {
		mavenLocal()
		maven {
			url "https://res-lab.globaltruck.ru/api/v4/projects/${System.getenv('CI_PROJECT_ID')}/packages/maven"
			credentials(HttpHeaderCredentials) {
				name = 'Job-Token'
				value = System.getenv("CI_JOB_TOKEN")
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:2.7.3'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.7.3'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.hibernate:hibernate-core'
}

tasks.named('test') {
	useJUnitPlatform()
}

